grammar com.isb.bks.ivr.voice.dsl.VoiceDsl with org.eclipse.xtext.common.Terminals

import "http://www.isb.com/bks/ivr/voice/dsl/VoiceDsl/1.0"

VoiceDsl:
	InputDsl
	| MenuDsl
	| PromptDsl
	| RecordDsl
	| TransferDsl
    ;

PromptDsl:
	'output' name = ID 
       ((configuration = OutputConfiguration)? 
	    (conditions += Condition*))
  	     audios=Audios	
;

MenuDsl:
	'menu' name = ID
	   (configuration = InputConfiguration)? 
       ((grammars = Grammars)&
	    (conditions += Condition*))
	     audios=Audios
  	     outputs=Outputs
;

InputDsl:
	'input' name = ID
      (configuration = InputConfiguration)? 
 	   ((grammars = Grammars)&
	   (conditions += Condition*))
  	    audios=Audios	
;

RecordDsl:
	'record' name = ID 
	'{'
		(('filename' fileName = STRING)&
        (configuration = RecordConfiguration)?)
  	 '}'
	    conditions += Condition*
  	    audios=Audios
;

TransferDsl:
	{TransferDsl}
	typeTransfer=TypeTransfer name = ID 
	'{'
	   (('destination' destination = STRING )&
		('transferaudio' audioTransfer = STRING )?& 
		('maxtime' maxTime = STRING )?&
		('transferTimeout' timeout = STRING )?&
		(configuration = TransferConfiguration)?)
	'}'
       (conditions += Condition*)?
  	     audios=Audios	
;
	  
Grammars:
	{Grammars}
	'grammars' '{'
	       grammatics += Grammar*
	    '}'
;

Audios: 
	'audios' '{' 
	('main' ':' (
			mainAudios += ConditionalAudio+))? &
	('match' ':' (
			matchAudios += ConditionalAudio+))? &
	('nomatch' ':' (
		    noMatchAudios += ConditionalAudio+))? &
	('noinput' ':' (
		    noInputAudios += ConditionalAudio+))? &
		     
    '}'
;

Configuration:
	{Configuration}
	 '{'
	  configValue += ConfigValue*
	 '}'
;

OutputConfiguration returns Configuration:
	{Configuration}
	 '{'
	  configValue += ConfigValue*
	 '}'
;

InputConfiguration returns Configuration:
	{Configuration}
	 '{'
	  configValue += ConfigValue*
	 '}'
;


RecordConfiguration returns Configuration:
	{Configuration}
	  configValue += ConfigValue*
;

TransferConfiguration returns Configuration:
	{Configuration}
	  configValue += ConfigValue*
;


ConfigValue:
	name = QualifiedName (value=STRING | value=ConfigValueValue)
;

ConfigValueValue:
	INT ('ms'|'s')?| Float('ms'|'s')? | 'true' | 'false'
;

Grammar:
	mode = ('voice'|'dtmf'|'voicedtmf') expr ?= 'expr'? src = STRING
;

Condition:
	{Condition}
	'condition' name = ID condition = INT 
;

Outputs:
	'options' '{' 
  		  output+=Output+
	 '}'
;

Output:
	 name = ID ':' outputValue+=OutputValue (',' outputValue+=OutputValue)*
;

OutputValue:
	value=STRING | value='*'
;

SimpleAudio returns Audio:
	{Audio}
	'say' (('src' src=STRING)?
		(('wording' tts=STRING ) |
			('as' (interpretation=Interpretation) ('(' format=STRING ')')?  tts=STRING)
		)?
	)
;

enum Interpretation:
	number | boolean | currency | date | digits | phone | time
;

enum TypeTransfer:
	blind =  'blindTransfer' | consultation = 'consultationTransfer' | bridge ='bridgeTransfer'
;

ConditionalAudio returns Audio:
	{ConditionalAudio}
	'when' '('  condit=STRING ')'  '{' (simpleAudios+=SimpleAudio)+ '}'		                                      
    |SimpleAudio
 ;
 
 Float :
   (INT '.' INT)
;

QualifiedName :
  ID ('.' ID)*
;

