grammar com.isb.bks.ivr.voice.dsl.VoiceDsl with org.eclipse.xtext.common.Terminals

generate voiceDsl "http://www.isb.com/bks/ivr/voice/dsl/VoiceDsl"

InputElement :
	configuration = Configuration
	grammatics = Grammar?
  	audios = Audios?
;

Grammar:
	'grammar' mode = ('voice'|'dtmf') builtin ?= 'builtin'? src = STRING
;

Configuration:
	'inputname' name = ID ('maxNoInput' maxNoInput=INT)? ('maxNoMatch' maxNoMAtch=INT)? ('maxAttempts' maxAttempts=INT)?
;

Audios:
	'audios' '{' 
		initial = Initial &
		noMatch = NoMatch? &
		noInput = NoInput? &
		match = Match?
	 '}'
;

NoInput:
	'noinput' ':' (
		audio += ConditionalAudio+
	)
;

NoMatch:
	'nomatch' ':' (
		audio += ConditionalAudio+
	)
;

Match:
	'match' ':' (
		audio += SimpleAudio+
	)
;

Initial:
	{Initial}
	'initial' ':' (
		audio += SimpleAudio+
	)
	
;

SimpleAudio returns Audio:
	{Audio}
	'say' dontBargeIn ?= 'nobarge'?  (tts=STRING)? ('src' src=STRING)? 
;	

ConditionalAudio returns Audio:
	(condition = Condition '=>')? 
	(
		audio += SimpleAudio |
		'{' audio += SimpleAudio+ '}'
	)
 ;

Condition:
	'count' count = INT
;