grammar com.isb.bks.ivr.voice.dsl.VoiceDsl with org.eclipse.xtext.xbase.Xbase

import "platform:/resource/com.isb.bks.ivr.voice.dsl/model/VoiceDsl.ecore"


InputElement:
	('inputname' name = ID           
    'configuration' '{'
	   configuration = Configuration 
	'}'
	'grammars' '{'
	   grammatics += Grammar*
	'}'
	properties += Property*
	conditions += Condition*
  	'audios' '{' 
  		defAudios=DefAudios	
	 '}') 
	 
	 
     | ('promptname' name = ID 
     properties += Property*
	 conditions += Condition*
  	 'audios' '{' 
  		defAudios=DefAudios	
	 '}') 


    | ('menuname' name = ID
	'configuration' '{'
	   configuration = Configuration 
	'}'
    'grammars' '{'
	grammatics += Grammar*
	'}'
	properties += Property*
	conditions += Condition*
  	'outputs' '{' 
  		outputs+=Outputs+
	 '}') 
	 
;

DefAudios:
	('main' ':' (
			mainAudios += ConditionalAudio+))? &
	('match' ':' (
			matchAudios += ConditionalAudio+))? &
	('nomatch' ':' (
		    noMatchAudios += ConditionalAudio+))? &
	('noinput' ':' (
		     noInputAudios += ConditionalAudio+))? 
;

Configuration:
	  ('maxNoInput' maxNoInput=INT)? & 
	  ('maxNoMatch' maxNoMatch=INT)? & 
	  ('maxAttempts' maxAttempts=INT)? &
	  ('confidence' confidence=FLOAT)? & 
	  ('bargein' ((bargein?="true")|'false') )? &
	  ('timeout' timeout=INT ('s'|'ms'))? &
	  ('interdigittimeout' interdigittimeout=INT ('s'|'ms'))? 
;

Grammar:
	mode = ('voice'|'dtmf'|'voicedtmf') expr ?= 'expr'? src = STRING
;

Property:
	'property' name = ID value = STRING
;

Condition:
	{Condition}
	'condition' name = ID condition = INT 
;

Outputs:
	 name = ID ':' output+=Output (',' output+=Output)*
;

Output:
	value =STRING | '*'
;

SimpleAudio returns Audio:
	{Audio}
	'say' dontBargeIn ?= 'nobargein'? (('src' src=STRING) ('wording' tts=STRING)? |
		                               (tts=STRING)	)
;	

ConditionalAudio returns Audio:
	{ConditionalAudio}
	(( 'when' '('  condit=XExpression) ')' '{' 
		('say' dontBargeIn ?= 'nobargein'? (('src' src=STRING) ('wording' tts=STRING)? |
		                                    (tts=STRING)	) )+ '}')		                                      
    |SimpleAudio
 ;
 
 terminal FLOAT :
   (INT '.' INT)
;

