grammar com.isb.bks.ivr.voice.dsl.VoiceDsl with org.eclipse.xtext.common.Terminals

import "platform:/resource/com.isb.bks.ivr.voice.dsl/model/VoiceDsl.ecore"

InputElement :
	'inputname' name = ID (('maxNoInput' maxNoInput=INT)? & ('maxNoMatch' maxNoMatch=INT)? & ('maxAttempts' maxAttempts=INT)?)
	grammatics += Grammar*
	conditions += Condition*
  	'audios' '{' (
  		('initial' ':' (
			initialAudios += SimpleAudio+))? &
		('nomatch' ':' (
		    noMatchAudios += ConditionalAudio+))? &
		('noinput' ':' (
		     noInputAudios += ConditionalAudio+))? )
	 '}'
;
 
Grammar:
	'grammar' mode = ('voice'|'dtmf') expr ?= 'expr'? src = STRING
;

Condition:
	{Condition}
	'condition' condition = INT name = ID
;

SimpleAudio returns Audio:
	{Audio}
	'say' dontBargeIn ?= 'nobargein'?  (tts=STRING)? ('src' src=STRING)? 
;	

ConditionalAudio returns Audio:
	{ConditionalAudio}
	('if' condition = [Condition] ) '{' ('say' dontBargeIn ?= 'nobargein'?  (tts=STRING)? ('src' src=STRING)?)+ '}'
 ;